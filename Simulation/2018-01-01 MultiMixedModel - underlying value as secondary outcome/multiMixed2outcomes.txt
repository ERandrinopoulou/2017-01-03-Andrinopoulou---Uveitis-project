model
{
    for (i in 1:N) {
        for (j in offset[i]:(offset[i + 1] - 1)) {
            muy[j] <- inprod(betas1[1:ncX], X[j, 1:ncX]) + inprod(b[i, 1:ncZ], Z[j, 1:ncZ]) + 
                      alphas * muy2[j] 
            y[j] ~ dnorm(muy[j], tau)
        }
        for (j in offset[i]:(offset[i + 1] - 1)) {
            muy2[j] <- inprod(betas2[1:ncX2], X2[j, 1:ncX2]) + inprod(b[i, (ncZ + 1):(ncZ + ncZ2)], Z2[j, 1:ncZ2])
            Pr[j] <- max(1.00000E-05, min(0.99999, (exp(muy2[j])/(1 + exp(muy2[j])))))
            y2[j] ~ dbin(Pr[j], 1)
        }
        b[i, 1:nb] ~ dmnorm(mu0[], inv.D[, ])
    }
    betas1[1:ncX] ~ dmnorm(priorMean.betas1[], priorTau.betas1[, ])
    betas2[1:ncX2] ~ dmnorm(priorMean.betas2[], priorTau.betas2[, ])
    alphas ~ dnorm(priorMean.alphas, priorTau.alphas)
    tau ~ dgamma(priorA.tau, priorB.tau)
    inv.D[1:nb, 1:nb] ~ dwish(priorR.D[, ], priorK.D)
}
